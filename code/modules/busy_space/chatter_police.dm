/datum/atc_chatter/policescan/squak()
	//Control scan event: soft outcome
	switch(phase)
		if(1)
			SSatc.msg("[combined_first_name], [callname], your [pick("ship","vessel","starship")] has been flagged for routine inspection. Hold position and prepare to be scanned.")
			next()
		if(2)
			var/confirm = pick("Understood","Roger that","Affirmative","Very well","Copy that")
			SSatc.msg("[confirm] [callname], holding position.","[comm_first_name]")
			next()
		if(3)
			SSatc.msg("Your compliance is appreciated, [combined_first_name]. Scan commencing.")
			next()
		if(4)
			var/complain = pick("I hope this doesn't take too long.","Can we hurry this up?","Make it quick.","This better not take too long.","Is this really necessary?","I'm sure you'll find everything to be in order, Control.")
			SSatc.msg(complain,"[comm_first_name]")
			next(10)
		else
			var/completed = pick("You're free to proceed.","Everything looks fine, carry on.","You're clear, move along.","Apologies for the delay, you're clear.","Switch to channel [SSatc.sdfchannel] and await further instruction.")
			SSatc.msg("[combined_first_name], [callname]. Scan complete. [completed]")
			finish()

/datum/atc_chatter/policeflee/squak()
	//Control scan event: hard outcome
	switch(phase)
		if(1)
			SSatc.msg("Unknown [pick("ship","vessel","starship")], [callname], identify yourself and submit to a full inspection. Flying without an active transponder is a violation of interstellar shipping regulations.")
			next()
		if(2)
			var/uhoh = pick("No can do chief, we got places to be.","Sorry but we've got places to be.","Not happening.","Ah fuck, who ratted us out this time?!","You'll never take me alive!","Hey, I have a cloaking device! You can't see me!","I'm going to need to ask for a refund on that stealth drive...","I'm afraid I can't do that, Control.","Ah |hell|.","Fuck!","This isn't the ship you're looking for.","Well. This is awkward.","Uh oh.","I surrender!","Ah f- |ditch the containers!| +Now!+","Unless you have something a little +bigger+ in your torpedo tubes, we're |not| turning around!")
			SSatc.msg("[uhoh]","Unknown Vessel")
			next()
		else
			SSatc.msg("[using_map.starsys_name] Defense Control to all local assets: vector to interdict and detain [prefix], temporary callsign |[shipname]|. Control out.","[using_map.starsys_name] Defense Control")
			finish()

/datum/atc_chatter/policeshipscan/squak()
	//SDF scan event: soft outcome
	switch(phase)
		if(1)
			SSatc.msg("[combined_second_name], [combined_first_name], your [pick("ship","vessel","starship")] has been flagged for routine inspection. Hold position and prepare to be scanned.","[comm_first_name]")
			next()
		if(2)
			var/confirm = pick("Understood","Roger that","Affirmative")
			SSatc.msg("[confirm] [combined_first_name], holding position.","[comm_second_name]")
			next()
		if(3)
			SSatc.msg("Your compliance is appreciated, [combined_second_name]. Scan commencing.","[comm_first_name]")
			next(2)
		if(4)
			var/complain = pick("I hope this doesn't take too long.","Can we hurry this up?","Make it quick.","This better not take too long.","Is this really necessary?","I'm sure you'll find everything to be in order, officer.")
			SSatc.msg(complain,"[comm_second_name]")
			next()
		else
			var/completed = pick("You're free to proceed.","Everything looks fine, carry on.","You're clear. Move along.","Apologies for the delay, you're clear.","Switch to channel [SSatc.sdfchannel] and await further instruction.")
			SSatc.msg("[combined_second_name], [combined_first_name]. Scan complete. [completed]","[comm_first_name]")
			finish()

/datum/atc_chatter/policeshipflee/squak()
	//SDF scan event: hard outcome
	switch(phase)
		if(1)
			SSatc.msg("Unknown [pick("ship","vessel","starship")], [combined_second_name], identify yourself and submit to a full inspection. Flying without an active transponder is a violation of interstellar shipping regulations.","[comm_second_name]")
			next()
		if(2)
			var/uhoh = pick("No can do chief, we got places to be.","Sorry but we've got places to be.","Not happening.","Ah fuck, who ratted us out this time?!","You'll never take me alive!","Hey, I have a cloaking device! You can't see me!","I'm going to need to ask for a refund on that stealth drive...","I'm afraid I can't do that, |[shipname]|.","Ah |hell|.","Fuck!","This isn't the ship you're looking for.","Well. This is awkward.","Uh oh.","I surrender!","Ah f- |ditch the containers!| +Now!+","Unless you have something a little +bigger+ in your torpedo tubes, we're |not| turning around!")
			SSatc.msg("[uhoh]","Unknown Vessel")
			next()
		if(3)
			SSatc.msg("[using_map.starsys_name] Defense Control,  [combined_second_name]. We have a [prefix] here, please advise.","[comm_second_name]")
			next()
		else
			SSatc.msg("Defense Control copies, [combined_second_name], reinforcements are en route. Switch further communications to encrypted band [SSatc.sdfchannel].","[using_map.starsys_name] Defense Control")
			finish()

/datum/atc_chatter/policeshipcombat/squak()
	//SDF scan event: engage primary in combat! fairly rare since it needs a pirate/vox + SDF roll
	switch(phase)
		if(1)
			var/battlestatus = pick("requesting reinforcements.","we need backup! Now!","holding steady.","we're holding our own for now.","we have them on the run.","they're trying to make a run for it!","we have them right where we want them.","we're badly outgunned!","we have them outgunned.","we're outnumbered here!","we have them outnumbered.","this'll be a cakewalk.",10;"notify their next of kin.")
			SSatc.msg("[using_map.starsys_name] Defense Control,  [combined_second_name], engaging [combined_first_name] [pick("near route","in sector")] [rand(1,100)], [battlestatus]","[comm_second_name]")
			next()
		else
			SSatc.msg("[using_map.starsys_name] Defense Control copies, [combined_second_name]. Keep us updated.","[using_map.starsys_name] Defense Control")
			finish()

/datum/atc_chatter/hostiledetected/squak()
	//DefCon event: hostile found
	switch(phase)
		if(1)
			SSatc.msg("This is [using_map.starsys_name] Defense Control to all SDF assets. Priority update follows.","[using_map.starsys_name] Defense Control")
			next()
		else
			var/orders = pick("Engage on sight","Engage with caution","Engage with extreme prejudice","Engage at will","Search and destroy","Bring them in alive, if possible","Interdict and detain","Keep your eyes peeled","Bring them in, dead or alive","Stay alert")
			SSatc.msg("Be on the lookout for [short_first_name], last sighted [pick("near route","in sector","near sector")] [rand(1,100)]. [orders]. DefCon, out.","[using_map.starsys_name] Defense Control")
			finish()
